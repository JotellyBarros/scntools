#!/usr/local/bin/guash
#
# mkeconet.gua
#
#     This script creates a Pajek network file from a CSV file,
#     containing the actors chromosomes.
#
# Copyright (C) 2013 Tereza Kelly Gomes Carneiro,
#               Roberto Luiz Souza Monteiro,
#               Hernane Borges de Barros Pereira.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
#
# RCS: @(#) $Id: mkeconet.gua,v 1.0 2013/03/24 12:18:00 monteiro Exp $
#

function CNA_SaveFile(filename, adj, type = "edges") {
    dim_adj = dim(adj)
    dim_i = dim_adj[0]
    dim_j = dim_adj[1]

    # Each line in a Pajek files must terminate with CR+LF.
    # Windows will automaticly converts "\n" to "\r\n", but
    # Linux will not.
    if ($SYS_HOST == "linux") {
        new_line = "\r\n"
    } else {
        new_line = "\n"
    }

    fp = fopen(filename, "w")

    # Save vertices...
    fputs("*Vertices " + (dim_i - 1) + new_line, fp)
    for (i = 1; i < dim_i; i = i + 1) {
        fputs(i + " \"" + adj[i, 0] + "\"" + new_line, fp)
    }

    # Save edges...
    if (type == "edges") {
        fputs("*Edges" + new_line, fp)
        for (i = 1; i < dim_i; i = i + 1) {
            for (j = 1; j < dim_j; j = j + 1) {
                if (adj[i, j] != 0) {
                    fputs(i + " " + j + " " + adj[i, j] + new_line, fp)
                }
            }
        }
    # Save arcs...
    } elseif (type == "arcs") {
        fputs("*Arcs" + new_line, fp)
        for (i = 1; i < dim_i; i = i + 1) {
            for (j = 1; j < dim_j; j = j + 1) {
                if (adj[i, j] != 0) {
                    fputs(i + " " + j + " " + adj[i, j] + new_line, fp)
                }
            }
        }
    # Save matrix...
    } elseif (type == "matrix") {
        fputs("*Matrix" + new_line, fp)
        for (i = 1; i < dim_i; i = i + 1) {
            for (j = 1; j < dim_j; j = j + 1) {
                fputs(" " + adj[i, j], fp)
            }
            fputs(new_line, fp)
        }
    }

    fp = fclose(fp)
}

#
# Configuration variables.
#
input_file = "noname"
output_file = "output.net"
output_file_type = "edges"
report_file = "output.rep"
separator = ";"
label = 0
patterns = ""
# Number of individuals.
nindividuals = 0
# Number of genes.
ngenes = 0
# Perform exact genes comparation.
exact = FALSE
# Genes are codded as string.
gs = FALSE
# Affinity tax.
atax = 0.25

#
# Main program.
#
if (argc < 3) {
    println("Usage: mkeconet option1 value1 [option1 value1]")
    exit
}

i = 2

while (i < argc) {
    if ((argv[i] == "-h") || (argv[i] == "--help")) {
        println("Usage: mkeconet option1 value1 [option1 value1]")
        println("-h    --help    show the options sumary")
        println("      --gs      genes are codded as string")
        println("-i              input file")
        println("-o              output file")
        println("-t              output file type(default=\"edges\")")
        println("-r              report file")
        println("-s              column separator(default=\";\")")
        println("-c              column number of vertex label(default=0)")
        println("-p              chromosome patterns")
        println("-a              affinity tax(default = 0.25)")
        println("-x              exact genes comparation")
        exit(0)
    } elseif (argv[i] == "--gs") {
        gs = TRUE
    } elseif (argv[i] == "-i") {
        i = i + 1
        input_file = argv[i]
    } elseif (argv[i] == "-o") {
        i = i + 1
        output_file = argv[i]
    } elseif (argv[i] == "-t") {
        i = i + 1
        output_file_type = argv[i]
    } elseif (argv[i] == "-r") {
        i = i + 1
        report_file = argv[i]
    } elseif (argv[i] == "-s") {
        i = i + 1
        separator = argv[i]
    } elseif (argv[i] == "-c") {
        i = i + 1
        label = eval(argv[i])
    } elseif (argv[i] == "-p") {
        i = i + 1
        patterns = argv[i]
    } elseif (argv[i] == "-a") {
        i = i + 1
        atax = eval(argv[i])
    } elseif (argv[i] == "-x") {
        exact = TRUE
    }
    i = i + 1
} 

# Check if the input file name was suplied...
if (input_file == "noname") {
    println("Error: input file name is mandatory.")
    exit(1)
}

# Get the current time...
t1 = time();

# Get chromosomes patterns...
if (patterns != "") {
    record = split(trim(patterns), ";")
    
    foreach (record; n; v) {
        col = split(trim(v), "=")
        
        name = trim(col[0], " \"")
        value = trim(col[1], " \"")
        
        pattern[name] = value
    }
}

# Generate the Pajek network file...
fs = fopen(input_file, "r")

# Skip column names...
line1 = fgets(fs)

# Generate vertex chromosomes...
n = 1
while (!feof(fs)) {
    if((line1 = fgets(fs)) == NULL) {
        break
    }
    
    record1 = split(trim(line1, " \t\r\n"), separator)
    
    if (n == 1) {
        if (length(record1) > 1) {
            ngenes = length(record1) - 1
        }
    }
    
    # Get vertex label...
    vertice[n] = trim(record1[label], " \"")
    
    if (length(record1) > 1) {
        vertice[n] = vertice[n] + ","
    } else {
        break
    }
    
    foreach(record1; key; value) {
        if (key != label) {
            if (!gs) {
                gene = eval(value)
            } else {
                gene = value
            }            
            
            if (patterns != "") {
                vertice[n] = vertice[n] + pattern[gene]
            } else {
                if (!gs) {
                    vertice[n] = vertice[n] + toString(gene)
                } else {
                    vertice[n] = vertice[n] + gene
                }
            }
            
            if (key < (length(record1) - 1)) {
                vertice[n] = vertice[n] + ","
            }
        }
    }
    
    n = n + 1
}
n = n - 1

fs = fclose(fs)

nindividuals = n

# Create the network matrix...
adj = adj = matrix(0, nindividuals + 1, nindividuals + 1)

dim_adj = dim(adj)
dim_i = dim_adj[0]
dim_j = dim_adj[1]

# Generate vertex chromosomes...
for (i = 1; i <= nindividuals; i = i + 1) {
    adj[0, i] = vertice[i]
    adj[i, 0] = vertice[i]
}

# Calculate the affinity between two vertices and stores it inside the affinity matrix...
for (i = 1; i < dim_i; i = i + 1) {
    for (j = 1; j < dim_j; j = j + 1) {
        if (i != j) {
            node1 = split(adj[i, 0], ",")
            node2 = split(adj[0, j], ",")
            
            nattributes = length(node1) - 1
            
            valuegs = 1
            if (gs) {
                valuegs = 0
            }
            
            c = 0
            for (index = valuegs; index <= nattributes; index = index + 1) {
                if (!exact) {
                    if ((node1[index] == node2[index]) && ((node1[index] != 0) && (node2[index] != 0))) {
                        c = c + 1
                    }
                } else {
                    if (node1[index] == node2[index]) {
                        c = c + 1
                    }
                }
            }
            
            #println("nattributes gs= " + nattributes)
            if (gs) {
                affinity = c
            } else {
                affinity = (c * 1.0) / (nattributes * 1.0)
            }
            
            adj[i, j] = affinity
            #println(toString(node1) + " - " + toString(node2) + " - c = " + c + " - aff = " + affinity)
        }
    }
}

# Calculate statistics...
sx = 0
sx2 = 0
n = 0

min_affinity = 0
max_affinity = 0

for (i = 1; i < dim_i; i = i + 1) {
    for (j = 1; j < dim_j; j = j + 1) {
        # Calculate the frequency distribution, the min, max and average affinity...
        # Get the frequency distribution...
        try {
            freqdist[toString(adj[i, j])] = freqdist[toString(adj[i, j])] + 1
        } catch {
            freqdist[toString(adj[i, j])] = 1
        }
        if (min_affinity == 0) {
            min_affinity = adj[i, j]
        }
        if (max_affinity == 0) {
            max_affinity = adj[i, j]
        }
        if (min_affinity > adj[i, j]) {
            min_affinity = adj[i, j]
        }
        if (max_affinity < adj[i, j]) {
            max_affinity = adj[i, j]
        }
        
        x = adj[i, j]
        
        sx = sx + x
        sx2 = sx2 + x * x
        
        n = n + 1
    }
}

# Calculate the percentage frequency distribution...
hist = matrix(0, length(freqdist), 3)
dim_hist = dim(hist)

i = 0
foreach(freqdist; v; f) {
    hist[i, 0] = eval(v)
    hist[i, 1] = f
	i = i + 1
}

sumfreqs = sum(hist, 0, 1, dim_hist[0] - 1, 1)

for (i = 0; i < dim_hist[0]; i = i + 1) {
    hist[i, 2] = (hist[i, 1] / (sumfreqs * 1.0)) * 100.0
}

# Sort the frequency distribution...
for (i = 0; i < dim_hist[0] - 1; i = i + 1) {
    for (j = i; j < dim_hist[0]; j = j + 1) {
        if (hist[i, 0] > hist[j, 0]) {
            v = hist[i, 0]
            hist[i, 0] = hist[j, 0]
            hist[j, 0] = v
            
            f = hist[i, 1]
            hist[i, 1] = hist[j, 1]
            hist[j, 1] = f
            
            p = hist[i, 2]
            hist[i, 2] = hist[j, 2]
            hist[j, 2] = p
        }
    }
}

avg_affinity = sx / n
deviation = sqrt((sx2 - (sx * sx) / n) / (n - 1))

# Connect or disconnect nodes based on the affinity tax...        
for (i = 1; i < dim_i; i = i + 1) {
    for (j = 1; j < dim_j; j = j + 1) {
        if (i != j) {
            if (!gs) { #Inseri esta condição, pois se gs=TRUE adj[i, j] ficará como está
                if (adj[i, j] >= atax) {
                    adj[i, j] = 1
                } else {
                    adj[i, j] = 0
                }
            } 
        } else {
            adj[i, j] = 0
        }
    }
}

# Save results...
fr = fopen(report_file, "w")

fputs("Number of individuals: " + nindividuals + "\n", fr)
fputs("Number of genes: " + ngenes + "\n", fr)
fputs("Affinity tax: " + atax + "\n\n", fr)

fputs("Min. affinity value: " + min_affinity + "\n", fr)
fputs("Avg. affinity value: " + avg_affinity + "\n", fr)
fputs("Max. affinity value: " + max_affinity + "\n", fr)
fputs("Deviation: " + toString(deviation) + "\n", fr)
fputs("Events: " + n + "\n\n", fr)

fputs("Frequency distribution:\n", fr)

line = sprintf("%15s%15s%15s\n", "Value", "Frequency", "Frequency(%)")
fputs(line, fr)

for (i = 0; i < dim_hist[0]; i = i + 1) {
    line = sprintf("%15.3f%15.0f%15.3f\n", hist[i, 0], hist[i, 1], hist[i, 2])
    fputs(line, fr)
}

fr = fclose(fr)

# Save the network file...
CNA_SaveFile(output_file, adj, output_file_type)

# Get the current time...
t2 = time();

# Show the elapsed time...
printf("Elapsed time to generate the network file %s: %.3f seconds\n\n", output_file, t2 - t1)

